plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'gay.nullby.carriername'
    compileSdk 33

    signingConfigs {
        release {
            if (project.properties['keyStore.release.storeFile'] != null) {
                storeFile file(project.properties['keyStore.release.storeFile'])
                storePassword project.properties['keyStore.release.storePassword']
                keyAlias project.properties['keyStore.release.keyAlias']
                keyPassword project.properties['keyStore.release.keyPassword']
            }
        }
    }

    defaultConfig {
        applicationId "gay.nullby.carriername"
        minSdk 28
        targetSdk 33
        versionCode 2
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // Download android.jar from the following url and put it in the root directory of the project
    // https://github.com/Reginer/aosp-android-jar/raw/refs/heads/main/android-33/android.jar
    final androidJar = file('../android.jar')
    if (androidJar.exists()) {
        compileOnly files(androidJar)
    }

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    def shizuku_version = "13.1.0"
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
}